# Процедуры

## Процедура ДобавитьНаличиеНаСкладе(Команда)
Открывает форму "ФормаДобавления".

```
Оповещение = Новый ОписаниеОповещения("ОбновитьЭлементФормы", ЭтаФорма);
ОткрытьФорму("Документ.УчетОтработанныхРанееТМЦ.Форма.ФормаДобавления",,,,,,Оповещение,РежимОткрытияОкнаФормы.Независимый);
```

## Процедура ОбновитьЭлементФормы(Результат, ДополнительныеПараметры) Экспорт
Обновляет сведения об остатках на складе после добавления товаров с помощью кнопки "Добавить наличие на складе". 

```
Элементы.ДанныеРегистра.Обновить();
```

## Процедура Обновить(Команда)
Обновляет динамические списки формы при нажатии на кнопку "Обновление".

```
Элементы.ДанныеРегистра.Обновить();
Элементы.Список.Обновить();
```

## Процедура ДанныеРегистраВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
Работает при двойном нажатии на строку таблицы "ДанныеРегистра" и добавляет строку с соответствующими значениями в таблицу "ВыборДляПередачи".

<details>
<summary> Код </summary>
    
    Если Элементы.ДанныеРегистра.ТолькоПросмотр = Ложь Тогда
        Если ЗначениеЗаполнено(Элементы.ДанныеРегистра.ТекущиеДанные.СерийныйНомер) 
            И ЭтотОбъект.ВыборДляПередачи.НайтиСтроки(Новый Структура("СерийныйНомер", Элементы.ДанныеРегистра.ТекущиеДанные.СерийныйНомер)).Количество() > 0 Тогда
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = "Номенклатура с таким серийным номером уже выбрана";
            Сообщение.Сообщить();
        Иначе 	
            Передача = ВыборДляПередачи.Добавить(); 
            Передача.Номенклатура = Элементы.ДанныеРегистра.ТекущиеДанные.НаименованиеТовара;
            Передача.Количество = 1;
            Передача.Организация = Элементы.ДанныеРегистра.ТекущиеДанные.Организация;
            Если ЗначениеЗаполнено(Элементы.ДанныеРегистра.ТекущиеДанные.СерийныйНомер) Тогда
                Передача.СерийныйНомер = Элементы.ДанныеРегистра.ТекущиеДанные.СерийныйНомер;
                Передача.РучнойВвод = Ложь;
            Иначе
                Передача.РучнойВвод = Истина;
            КонецЕсли;
            Элементы.Товары.ТолькоПросмотр = Истина;
        КонецЕсли;
    КонецЕсли;	
</details>

## Процедура СписокПриАктивизацииСтроки(Элемент)
При нажатии на строку таблицы "Список" заполняется поле "Исполнитель" и отменяются все действия, если во время перезакрепления пытаются сменить исполнителя таким образом.

<details>
<summary> Код </summary>
	
	Если Элементы.Список.ВыделенныеСтроки.Количество() > 0 Тогда
		Если Элементы.ДанныеРегистра.ТолькоПросмотр = Ложь Тогда
			Исполнитель = Элементы.Список.ТекущиеДанные.ФИО;    
		ИначеЕсли Элементы.Товары.ТекущаяСтрока = Неопределено 
			ИЛИ ЗначениеЗаполнено(ВыборДляПередачи[0].СерийныйНомер) 
			И НЕ Элементы.Список.ТекущиеДанные.Товары.НайтиСтроки(Новый Структура("Номенклатура, СерийныйНомер",
			ВыборДляПередачи[0].Номенклатура, ВыборДляПередачи[0].СерийныйНомер)).Количество() > 0 Тогда
			
			ВыборДляПередачи.Очистить();
			Элементы.ДанныеРегистра.ТолькоПросмотр = Ложь;
			Сообщить("Выбирать позиции для перезакрепления можно только по одному сотруднику!");
		КонецЕсли;
	КонецЕсли;

</details>

## Процедура Перезакрепить(Команда)
Работает при нажатии на кнопку "Перезакрепить", вызывает серверную процедуру по условию.

<details>
<summary> Код </summary>
	
	Если Элементы.ДанныеРегистра.ТолькоПросмотр = Ложь ИЛИ ВыборДляПередачи.Количество() = 0 Тогда
		Сообщить("Товары выбраны некорректно!");
	ИначеЕсли НЕ Элементы.Список.ТекущиеДанные.ФИО = Исполнитель И ЗначениеЗаполнено(Исполнитель) Тогда
		ПерезакрепитьНаСервере();	
	Иначе
		Сообщить("Для перезакрепления выберите другого исполнителя!");
	КонецЕсли;
</details>

## Процедура ПерезакрепитьНаСервере()
Выполняет алгоритм перезакрепления товаров, происходит вызов процедуры возврата и передачи.

<details>
<summary> Код </summary>
	
	ТЗСтрок = ВыборДляПередачи.Выгрузить();
	Индекс = 0;
	Пока Индекс <> ТЗСтрок.Количество() Цикл
		Строчка = ТЗСтрок[Индекс];
		ОИТС_УчетТМЦ.ВозвратНаСклад(Элементы.Список.ТекущаяСтрока, ТЗСтрок, Строчка);
	КонецЦикла;
	ДокУчетТМЦ = Элементы.Список.ТекущаяСтрока.ПолучитьОбъект();
	Для Каждого Выбор Из ВыборДляПередачи Цикл
		НайденныеСтроки = ДокУчетТМЦ.Товары.НайтиСтроки(Новый Структура("Номенклатура, СерийныйНомер, ВариантВнесения", 
																		   Выбор.Номенклатура, Выбор.СерийныйНомер, Выбор.ВариантВнесения));
		ДокУчетТМЦ.Товары.Удалить(НайденныеСтроки[0]);
	КонецЦикла;
	ДокУчетТМЦ.Записать();
	ПередатьНаСервере();
	Сообщить("Выбранные позиции успешно перезакреплены!");	
</details>

## Процедура Сборка(Команда)
Проверяет обязательные для сборки условия и вызывает серверную процедуру.

<details>
<summary> Код </summary>
	
	Если Элементы.ДанныеРегистра.ТолькоПросмотр = Истина ИЛИ ВыборДляПередачи.Количество() = 0 Тогда
		Сообщить("Товары выбраны некорректно!");
		Возврат;
	КонецЕсли;
	Если Элементы.НоменклатураСборки.Видимость = Ложь Тогда
		Элементы.НоменклатураСборки.Видимость = Истина;
	ИначеЕсли Элементы.НоменклатураСборки.Видимость = Истина И ЗначениеЗаполнено(Элементы.НоменклатураСборки.ТекстРедактирования) Тогда
		СборкаНаСервере();	
	Иначе 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Необходимо выбрать номенклатуру!";
		Сообщение.Поле = "НоменклатураСборки";
		Сообщение.Сообщить();	
	КонецЕсли;
</details>

## Процедура СборкаНаСервере()
Создание документа "СборкаТоваров", фиксация изменений в регистре сведений.

<details>
<summary> Код </summary>
	
	ПроверкаОрганизации = ВыборДляПередачи.Выгрузить();
	ПроверкаСерийногоНомера = ВыборДляПередачи.Выгрузить(); 
	ПроверкаОрганизации.Свернуть("Организация, СерийныйНомер");
	ПроверкаСерийногоНомера.Свернуть("СерийныйНомер");
	Если ПроверкаОрганизации.Количество() = 1 И ЗначениеЗаполнено(ПроверкаОрганизации[0].Организация) Тогда
		ИндексСтроки = 0;
		Док = Документы.УчетОтработанныхРанееТМЦ.СоздатьДокумент();
		СсылкаНового = Документы.УчетОтработанныхРанееТМЦ.ПолучитьСсылку();
		Док.УстановитьСсылкуНового(СсылкаНового);
		СборкаТоваров = Документы.СборкаТоваров.СоздатьДокумент();
		СсылкаНового = Документы.СборкаТоваров.ПолучитьСсылку();
		СборкаТоваров.УстановитьСсылкуНового(СсылкаНового);
		СборкаТоваров.Дата = ТекущаяДата();
		СборкаТоваров.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СборкаТоваров;
		СборкаТоваров.Склад = Справочники.Склады.НайтиПоНаименованию("Отдел ИТС (администрирование)");
		СборкаТоваров.Организация = ВыборДляПередачи[0].Организация;
		СборкаТоваров.Номенклатура = НоменклатураСборки;
		СборкаТоваров.КоличествоУпаковок = 1;
		СборкаТоваров.Количество = 1;
		СборкаТоваров.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным;
		СборкаТоваров.Статус = Перечисления.СтатусыСборокТоваров.СобраноРазобрано;
		Для Каждого Товар Из ВыборДляПередачи Цикл	
			ЗапросОстатков = ЗапроситьОстатки(Товар.Номенклатура, Товар.Организация);
			ИндексСтроки = ИндексСтроки + 1; 
			Если ЗапросОстатков.Количество() >= Товар.Количество Тогда
				НоваяСтрока = СборкаТоваров.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Товар);
				НоваяСтрока.КоличествоУпаковок = Товар.Количество;
				ОИТС_УчетТМЦ.ЗаписьВРегистрСведений(Док.ПолучитьСсылкуНового(), 
				Новый Структура("Номенклатура, СерийныйНомер, Количество", Товар.Номенклатура, 
				Товар.СерийныйНомер, Товар.Количество), "ИспользовалиДляСборки", СборкаТоваров.ПолучитьСсылкуНового(), ИндексСтроки);
			Иначе 
				Сообщить(Строка(Товар.Номенклатура) + "нет на складе в указанном количестве");
				Возврат;
			КонецЕсли;
		КонецЦикла;
		СборкаТоваров.Записать(РежимЗаписиДокумента.Проведение);
		ВыборДляПередачи.Очистить();
		Элементы.ДанныеРегистра.Обновить();
		Элементы.НоменклатураСборки.Видимость = Ложь;
		Элементы.Товары.ТолькоПросмотр = Ложь;
		Сообщить("Указанная номенклатура успешно собрана!");
	ИначеЕсли  ПроверкаСерийногоНомера.Количество() > 1 Тогда 
		Сообщить("Для комплектующих не указывается серийный номер!")
	ИначеЕсли Не ЗначениеЗаполнено(ПроверкаОрганизации[0].Организация) Тогда
		Сообщить("Выберите позиции с заполненным полем ""Организация""!");
	Иначе	
		Сообщить("Необходимо выбирать позиции из одной организации!");
	КонецЕсли;		
</details>

## Процедура ПередатьНаСервере()
При отсутствии документа "УчетОтработанныхРанееТМЦ" по выбранному исполнителю создает его. Выполняет основной алгоритм, который состоит из трех веток с приоритетностью в указанном порядке:
1. Создание документа внутреннего потребления "ПередачаВЭксплуатацию", если товар закреплен за организацией исполнителя;
2. Движение по регистру накоплений с регистратором "<Объект не найден>...", если товар на складе без привязке к организации;
3. Создание документа внутреннего потребления "СписаниеНаРасходы", если товар из другой организации.

На каждую переданную позицию создается запись в регистре сведений "ФиксацияИзменений". В 1 и 2 случаях расход в регистре накоплений создается в модуле соответствующих документов.

<details>
<summary> Код </summary>
	
	//Условия на корректность работы с формой
	Для Каждого Товар Из ВыборДляПередачи Цикл
		Если НЕ ЗначениеЗаполнено(Товар.СерийныйНомер) Тогда
			Сообщить("Поле ""Серийный номер"" не заполнено!");     
			Возврат;
		КонецЕсли;
		ПроверкаПоСерийномуНомеру = ЗапроситьОстатки(Товар.Номенклатура, Справочники.Организации.ПустаяСсылка(), Товар.СерийныйНомер, 3);
		Если ПроверкаПоСерийномуНомеру.Количество() > 0 Тогда
			Товар.РучнойВвод = Ложь;
		КонецЕсли;
		ЗапросОстатков = ЗапроситьОстатки(Товар.Номенклатура, Справочники.Организации.ПустаяСсылка(), ?(Товар.РучнойВвод = Ложь, Товар.СерийныйНомер, ""), 1); 
		ОстатокНоменклатуры = ?(ЗапросОстатков.Количество() > 0, 
								ЗапросОстатков[0].КоличествоОстаток - ВыборДляПередачи.НайтиСтроки(Новый Структура("Номенклатура, СерийныйНомер", Товар.Номенклатура, ?(Товар.РучнойВвод = Ложь, Товар.СерийныйНомер, ""))).Количество(), 
								0 - ВыборДляПередачи.НайтиСтроки(Новый Структура("Номенклатура, СерийныйНомер", Товар.Номенклатура, ?(Товар.РучнойВвод = Ложь, Товар.СерийныйНомер, ""))).Количество());
		Если ОстатокНоменклатуры >= 0 Тогда
			Продолжить;
		Иначе
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Номенклатуры " + """" + Строка(Товар.Номенклатура) + """" 
							  + " нет на складе в указанном количестве, не хватет: " + ?(ОстатокНоменклатуры >= 0, ОстатокНоменклатуры, -ОстатокНоменклатуры);
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
	КонецЦикла;
	//Необходимо для корректной передачи ссылки на документ
	СсылкаНовогоДокументаТМЦ = Документы.УчетОтработанныхРанееТМЦ.ПустаяСсылка();
	
	//Создание документа УчетОтработанныхРанееТМЦ
	Если Документы.УчетОтработанныхРанееТМЦ.НайтиПоРеквизиту("ФИО", Исполнитель) <> Документы.УчетОтработанныхРанееТМЦ.ПустаяСсылка() Тогда
		
		ДокументРедактирование = Документы.УчетОтработанныхРанееТМЦ.НайтиПоРеквизиту("ФИО", Исполнитель).Ссылка.ПолучитьОбъект();
		ДокументРедактирование.Дата = ТекущаяДата();		
	Иначе 
		ДокументРедактирование = Документы.УчетОтработанныхРанееТМЦ.СоздатьДокумент();
		ДокументРедактирование.Дата        = ТекущаяДата();
		ДокументРедактирование.ФИО         = ЭтотОбъект.Исполнитель;
		ДокументРедактирование.Организация = ЭтотОбъект.Исполнитель.Организация;
		СсылкаНовогоДокументаТМЦ = Документы.УчетОтработанныхРанееТМЦ.ПолучитьСсылку();
		ДокументРедактирование.УстановитьСсылкуНового(СсылкаНовогоДокументаТМЦ);
	КонецЕсли;
	
	ДанныеКоличества = Новый ТаблицаЗначений;
	ДанныеКоличества.Колонки.Добавить("Номенклатура");
	ДанныеКоличества.Колонки.Добавить("Количество"); 
	ДанныеКоличества.Колонки.Добавить("СерийныйНомер");
	ДанныеКоличества.Колонки.Добавить("РучнойВвод");
	
	//Счетчик количества по товарам передачи
	Для Каждого Товар Из ВыборДляПередачи Цикл
		НоваяСтрока = ДанныеКоличества.Добавить();
		НоваяСтрока.Номенклатура     = Товар.Номенклатура;
		НоваяСтрока.Количество       = Товар.Количество;
		НоваяСтрока.СерийныйНомер    = Товар.СерийныйНомер;
		НоваяСтрока.РучнойВвод       = Товар.РучнойВвод; 
	КонецЦикла;
	//Нужно для создания только одного документа ВнутреннееПотребление - Передача в эксплуатацию
	Передача = Ложь; 
	Для Каждого Товар из ВыборДляПередачи Цикл
		НайденнаяСтрока = ДанныеКоличества.НайтиСтроки(Новый Структура("Номенклатура, СерийныйНомер", Товар.Номенклатура, Товар.СерийныйНомер));
		Если НайденнаяСтрока[0].Количество > 0 Тогда
			//Создание документа ВнутреннееПотребление - Передача в эксплуатацию 
			ЗапросОстатков = ЗапроситьОстатки(Товар.Номенклатура, Исполнитель.Организация, ?(Товар.РучнойВвод = Ложь, Товар.СерийныйНомер, ""));
			Если ЗапросОстатков.Количество() > 0 Тогда
				Если Передача = Ложь Тогда
					ПередачаВЭксплуатацию = Документы.ВнутреннееПотребление.СоздатьДокумент();
					ПередачаВЭксплуатацию.Дата                  = ТекущаяДата();
					ПередачаВЭксплуатацию.Организация           = Исполнитель.Организация;
					ПередачаВЭксплуатацию.Подразделение         = Справочники.СтруктураПредприятия.НайтиПоНаименованию("Отдел ИТС");
					ПередачаВЭксплуатацию.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаВЭксплуатацию;   
					ПередачаВЭксплуатацию.Ответственный         = Пользователи.ТекущийПользователь();
					ПередачаВЭксплуатацию.Статус                = Перечисления.СтатусыВнутреннихПотреблений.Принято;
					ПередачаВЭксплуатацию.Склад                 = Справочники.Склады.НайтиПоНаименованию("Отдел ИТС (администрирование)");
					ПередачаВЭксплуатацию.ОИТС_СсылкаНаДокумент = ?(СсылкаНовогоДокументаТМЦ = Документы.УчетОтработанныхРанееТМЦ.ПустаяСсылка(), 
																	ДокументРедактирование.Ссылка, ДокументРедактирование.ПолучитьСсылкуНового());
					СсылкаНового = Документы.ВнутреннееПотребление.ПолучитьСсылку();
					ПередачаВЭксплуатацию.УстановитьСсылкуНового(СсылкаНового);
				КонецЕсли;
				Стр = ПередачаВЭксплуатацию.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(Стр, Товар);
				Если ЗапросОстатков[0].КоличествоОстаток - Товар.Количество >= 0 Тогда
					Стр.КоличествоУпаковок    = Товар.Количество;
				Иначе 
					Стр.КоличествоУпаковок    = ЗапросОстатков[0].КоличествоОстаток;
					Стр.Количество            = ЗапросОстатков[0].КоличествоОстаток;
				КонецЕсли;
				Стр.КатегорияЭксплуатации     = Справочники.КатегорииЭксплуатации.НайтиПоНаименованию("ТМЦ срок экспл. 12 мес");
				Стр.ФизическоеЛицо            = Исполнитель.ФизическоеЛицо;
				Стр.СтатьяРасходов            = Стр.КатегорияЭксплуатации.СтатьяРасходов;
				Стр.СерийныйНомер             = Товар.СерийныйНомер;
				Стр.АналитикаРасходов         = Справочники.Склады.НайтиПоНаименованию("Отдел ИТС (администрирование)");
				НайденнаяСтрока[0].Количество = НайденнаяСтрока[0].Количество - Стр.Количество;
				Передача   = Истина;  
				ОИТС_УчетТМЦ.ЗаписьВРегистрСведений(?(СсылкаНовогоДокументаТМЦ = Документы.УчетОтработанныхРанееТМЦ.ПустаяСсылка(), 
													ДокументРедактирование.Ссылка, ДокументРедактирование.ПолучитьСсылкуНового()), 
													Новый Структура("Номенклатура, СерийныйНомер, Количество", Товар.Номенклатура, 
													Товар.СерийныйНомер, Стр.Количество), "Передача", ПередачаВЭксплуатацию.ПолучитьСсылкуНового());
				СтрТМЦ = ДокументРедактирование.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрТМЦ, Товар);
				СтрТМЦ.ВариантВнесения = Перечисления.ВариантыВнесения.Передача; 
				Если НайденнаяСтрока[0].Количество = 0 Тогда
					ДанныеКоличества.Удалить(НайденнаяСтрока[0]);                                         
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если НайденнаяСтрока[0].Количество > 0 Тогда
			ЗапросОстатков = ЗапроситьОстатки(Товар.Номенклатура, Справочники.Организации.ПустаяСсылка(), ?(Товар.РучнойВвод = Ложь, Товар.СерийныйНомер, ""));
			Если ЗапросОстатков.Количество() > 0 Тогда
				//Создание движения по регистру 	
				Док = Документы.УчетОтработанныхРанееТМЦ.СоздатьДокумент();
				СсылкаНового = Документы.УчетОтработанныхРанееТМЦ.ПолучитьСсылку();
				Док.УстановитьСсылкуНового(СсылкаНового); 
				СсылкаНовойРеализации = Док.ПолучитьСсылкуНового();
	
				Набор = РегистрыНакопления.ТМЦНаСкладеОИТС.СоздатьНаборЗаписей();
				Набор.Отбор.Регистратор.Установить(СсылкаНовойРеализации );
				Набор.Прочитать();
				Движение = Набор.Добавить();
				Движение.ВидДвижения          = ВидДвиженияНакопления.Расход;
				Движение.Период               = ТекущаяДата();
				Движение.НаименованиеТовара   = Товар.Номенклатура;
				Если ЗапросОстатков[0].КоличествоОстаток - НайденнаяСтрока[0].Количество >= 0 Тогда
					Движение.Количество       = НайденнаяСтрока[0].Количество;
				Иначе 
					Движение.Количество       = ЗапросОстатков[0].КоличествоОстаток; 
				КонецЕсли;
				Движение.ЕдиницыИзмерения     = Товар.Номенклатура.ЕдиницаИзмерения;
				Движение.ОтработанРанее       = Истина;
				Движение.СсылкаНаДокумент     = ?(СсылкаНовогоДокументаТМЦ = Документы.УчетОтработанныхРанееТМЦ.ПустаяСсылка(), 
												  ДокументРедактирование.Ссылка, ДокументРедактирование.ПолучитьСсылкуНового()); 
				Если  ОИТС_УчетТМЦ.ЗапросИзРегистра(Товар.СерийныйНомер).Количество() > 0 Тогда
					Движение.СерийныйНомер     = Товар.СерийныйНомер;
				КонецЕсли;
				НайденнаяСтрока[0].Количество = НайденнаяСтрока[0].Количество - Движение.Количество;
				Набор.Записать();
				ОИТС_УчетТМЦ.ЗаписьВРегистрСведений(?(СсылкаНовогоДокументаТМЦ = Документы.УчетОтработанныхРанееТМЦ.ПустаяСсылка(), 
													ДокументРедактирование.Ссылка, ДокументРедактирование.ПолучитьСсылкуНового()), 
				   									Новый Структура("Номенклатура, СерийныйНомер, Количество", Товар.Номенклатура, 
													Товар.СерийныйНомер, Движение.Количество), "ОтработанРанее", СсылкаНовойРеализации); 
				СтрТМЦ = ДокументРедактирование.Товары.Добавить();									
				ЗаполнитьЗначенияСвойств(СтрТМЦ, Товар);
				СтрТМЦ.ВариантВнесения = Перечисления.ВариантыВнесения.ЗаписьВРегистре; 
			КонецЕсли;
			Если НайденнаяСтрока[0].Количество = 0 Тогда
				ДанныеКоличества.Удалить(НайденнаяСтрока[0]);
			КонецЕсли
		КонецЕсли;
	КонецЦикла;
	Если Передача = Истина Тогда
		ПередачаВЭксплуатацию.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	КопияТЗ = ДанныеКоличества.Скопировать();
	Для Каждого Данные Из ДанныеКоличества Цикл
		Если Данные.РучнойВвод = Истина Тогда
			Данные.СерийныйНомер = "";
		КонецЕсли;
	КонецЦикла;
	ДанныеКоличества.Свернуть("Номенклатура, СерийныйНомер", "Количество"); 
	Если ДанныеКоличества.Количество() > 0 Тогда
			ЗапросОстатков = ЗапроситьОстатки(ДанныеКоличества.ВыгрузитьКолонку("Номенклатура"), Справочники.Организации.ПустаяСсылка(), ДанныеКоличества.ВыгрузитьКолонку("СерийныйНомер"), 2);
			ЗапросОстатков.Сортировать("Организация");
			СортировкаПоОрганизации = ЗапросОстатков.НайтиСтроки(Новый Структура("Организация", ЗапросОстатков[0].Организация));
			Пока КопияТЗ.Количество() > 0 Цикл 
				//Создание документа ВнутреннееПотребление - Списание на расходы
				ВнутреннееПотребление = Документы.ВнутреннееПотребление.СоздатьДокумент();
				ВнутреннееПотребление.Дата                  = ТекущаяДата();
				ВнутреннееПотребление.Организация           = СортировкаПоОрганизации[0].Организация;
				ВнутреннееПотребление.Подразделение         = Справочники.СтруктураПредприятия.НайтиПоНаименованию("Отдел ИТС");
				ВнутреннееПотребление.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию;
				ВнутреннееПотребление.Склад                 = Справочники.Склады.НайтиПоНаименованию("Отдел ИТС (администрирование)");
				ВнутреннееПотребление.ОИТС_СсылкаНаДокумент = ?(СсылкаНовогоДокументаТМЦ = Документы.УчетОтработанныхРанееТМЦ.ПустаяСсылка(), 
																ДокументРедактирование.Ссылка, ДокументРедактирование.ПолучитьСсылкуНового()); 	
				СсылкаНового = Документы.ВнутреннееПотребление.ПолучитьСсылку();
				ВнутреннееПотребление.УстановитьСсылкуНового(СсылкаНового);
				Для Каждого ОстатокПоОрганизации Из СортировкаПоОрганизации Цикл
					ПоискСтрок = ДанныеКоличества.НайтиСтроки(Новый Структура("Номенклатура", ОстатокПоОрганизации.НаименованиеТовара));
					Если ПоискСтрок.Количество() > 0 Тогда
						Если ОстатокПоОрганизации.КоличествоОстаток - ПоискСтрок[0].Количество >= 0 Тогда
							КоличествоДляИндекса = ПоискСтрок[0].Количество;
						Иначе 
							КоличествоДляИндекса = ОстатокПоОрганизации.КоличествоОстаток;
						КонецЕсли;
						ПоискДляРегистра = КопияТЗ.НайтиСтроки(Новый Структура("Номенклатура", ОстатокПоОрганизации.НаименованиеТовара));
						Для Индекс = 0 По КоличествоДляИндекса - 1 Цикл
							Стр = ВнутреннееПотребление.Товары.Добавить();
							Стр.Номенклатура          = ПоискДляРегистра[Индекс].Номенклатура; 
							Стр.КоличествоУпаковок    = ПоискДляРегистра[Индекс].Количество;
							Стр.Количество            = ПоискДляРегистра[Индекс].Количество;
							Стр.КатегорияЭксплуатации = Справочники.КатегорииЭксплуатации.НайтиПоНаименованию("ТМЦ срок экспл. 12 мес");
							Стр.ФизическоеЛицо        = Исполнитель.ФизическоеЛицо;
							Стр.СерийныйНомер         = ПоискДляРегистра[Индекс].СерийныйНомер;
							ПоискСтрок[0].Количество  = ПоискСтрок[0].Количество - Стр.Количество;
							Если ПоискСтрок[0].Количество = 0 Тогда
								ДанныеКоличества.Удалить(ПоискСтрок[0]);
							КонецЕсли;
							
							ОИТС_УчетТМЦ.ЗаписьВРегистрСведений(?(СсылкаНовогоДокументаТМЦ = Документы.УчетОтработанныхРанееТМЦ.ПустаяСсылка(), 
																ДокументРедактирование.Ссылка, ДокументРедактирование.ПолучитьСсылкуНового()), 
																Новый Структура("Номенклатура, СерийныйНомер, Количество", ПоискДляРегистра[Индекс].Номенклатура, 
																ПоискДляРегистра[Индекс].СерийныйНомер, ПоискДляРегистра[Индекс].Количество),
																"Списание",ВнутреннееПотребление.ПолучитьСсылкуНового());
							СтрТМЦ = ДокументРедактирование.Товары.Добавить();
							ЗаполнитьЗначенияСвойств(СтрТМЦ, ПоискДляРегистра[Индекс]);
							СтрТМЦ.ВариантВнесения = Перечисления.ВариантыВнесения.Списание; 
							КопияТз.Удалить(ПоискДляРегистра[Индекс]); 
						КонецЦикла;
						ЗапросОстатков.Удалить(ОстатокПоОрганизации);
					КонецЕсли;
				КонецЦикла;
				ВнутреннееПотребление.Записать(РежимЗаписиДокумента.Проведение);
				Если ЗапросОстатков.Количество() > 0 Тогда
					СортировкаПоОрганизации = ЗапросОстатков.НайтиСтроки(Новый Структура("Организация", ЗапросОстатков[0].Организация));
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	ДокументРедактирование.Записать();
	Элементы.Список.Обновить();
	ВыборДляПередачи.Очистить();
	Элементы.ДанныеРегистра.Обновить();
	Элементы.ДанныеРегистра.ТолькоПросмотр = Ложь;
	Элементы.Товары.ТолькоПросмотр = Ложь;
</details>

## Процедура Передать(Команда)
Работает при нажатии на кнопку "Передать", вызывает серверную процедуру. В конце выводит сообщение с навигационной ссылкой на регистр накопления "ТМЦНаСкладеОИТС".

<details>
<summary> Код </summary>
	
	Если Элементы.ДанныеРегистра.ТолькоПросмотр = Истина Тогда
		Возврат;
	КонецЕсли;
	Если ВыборДляПередачи.Количество() = 0 Тогда
		Сообщить("Не выбрано какую номенклатуру надо передать!");
		Возврат;
	ИначеЕсли Элементы.Список.ВыделенныеСтроки.Количество() > 1 Тогда 
		Сообщить("Выбрано несколько строк ""ФИО"", выделите только одну и попробуйте снова!");
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(Исполнитель) = Ложь Тогда
		Сообщить("Поле ""Исполнитель"" не заполнено!");
		Возврат;
	Иначе
		ПередатьНаСервере(); 
		Если ВыборДляПередачи.Количество() = 0 Тогда	
			Оповещение = Новый ОписаниеОповещения("ОбработатьНажатие", ЭтаФорма);
			ПоказатьОповещениеПользователя("Выбранные позиции успешно переданы!", Оповещение, "Посмотреть подробности (ЖМИ!)",,СтатусОповещенияПользователя.Информация);
		КонецЕсли;
	КонецЕсли;
</details>

## Процедура ОбработатьНажатие(Параметры) Экспорт
Открывает форму списка регистра накоплений "ТМЦНаСкладеОИТС" после того, как пользователь нажмет на ссылку в сообщении об успешной передачи.

```
ОткрытьФорму("РегистрНакопления.ТМЦНаСкладеОИТС.ФормаСписка");
```

## Процедура РемонтНаСервере()  
Создает документ внутреннего потребления "СписаниеНаРасходы" с подходящими под ремонт статусами.

<details>
<summary> Код </summary>
	
	ПроверкаОрганизации = ВыборДляПередачи.Выгрузить();
	ПроверкаОрганизации.Свернуть("Организация"); 
	Если ПроверкаОрганизации.Количество() = 1 И ЗначениеЗаполнено(ПроверкаОрганизации[0].Организация) Тогда
		Док = Документы.УчетОтработанныхРанееТМЦ.СоздатьДокумент();
		СсылкаНового = Документы.УчетОтработанныхРанееТМЦ.ПолучитьСсылку();
		Док.УстановитьСсылкуНового(СсылкаНового); 
		СсылкаНовойРеализации = Док.ПолучитьСсылкуНового();
		
		ВнутреннееПотребление = Документы.ВнутреннееПотребление.СоздатьДокумент();
		ВнутреннееПотребление.Дата                  = ТекущаяДата();
		ВнутреннееПотребление.Организация           = ВыборДляПередачи[0].Организация;
		ВнутреннееПотребление.Подразделение         = Справочники.СтруктураПредприятия.НайтиПоНаименованию("Отдел ИТС");
		ВнутреннееПотребление.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию;
		ВнутреннееПотребление.Склад                 = Справочники.Склады.НайтиПоНаименованию("Отдел ИТС (администрирование)");
		СсылкаНового = Документы.ВнутреннееПотребление.ПолучитьСсылку();
		ВнутреннееПотребление.УстановитьСсылкуНового(СсылкаНового);
		Для Каждого Товар Из ВыборДляПередачи Цикл
			Стр = ВнутреннееПотребление.Товары.Добавить();
			Стр.Номенклатура          = Товар.Номенклатура; 
			Стр.КоличествоУпаковок    = Товар.Количество;
			Стр.Количество            = Товар.Количество;
			Стр.КатегорияЭксплуатации = Справочники.КатегорииЭксплуатации.НайтиПоНаименованию("Текущий и аварийный ремонт (счет )");
			Стр.СерийныйНомер         = Товар.СерийныйНомер;
			ОИТС_УчетТМЦ.ЗаписьВРегистрСведений(СсылкаНовойРеализации, Новый Структура("Номенклатура, СерийныйНомер, Количество", 
												Товар.Номенклатура, Товар.СерийныйНомер, Товар.Количество), "СписалиНаРасходы_Ремонт", ВнутреннееПотребление.ПолучитьСсылкуНового());
		КонецЦикла;
		ВнутреннееПотребление.Записать(РежимЗаписиДокумента.Проведение);
		ВыборДляПередачи.Очистить();
		Сообщить("Выбранные позиции успешно переданы в ремонт!");
		Элементы.ВыборДляПередачи.Обновить(); 
		Элементы.Товары.ТолькоПросмотр = Ложь;
	ИначеЕсли  ПроверкаОрганизации.Количество() > 1 Тогда
		Сообщить("Необходимо выбирать позиции из одной организации!");
	Иначе	
		Сообщить("Выберите позиции с заполненным полем ""Организация""!");
	КонецЕсли;
</details>

## Процедура Ремонт(Команда)
Работает при нажатии на кнопку "Ремонт", вызывает серверную процедуру.

```
	Если Элементы.ДанныеРегистра.ТолькоПросмотр = Истина ИЛИ ВыборДляПередачи.Количество() = 0 Тогда 
		Сообщить("Товары выбраны некорректно!");
		Возврат;
	КонецЕсли;
	РемонтНаСервере();
```

## Процедура ВыборДляПередачиПослеУдаления(Элемент)
После очистки "ВыборДляПередачи" возвращает возможность выбора в любой из двух таблиц.

```
	Если ВыборДляПередачи.Количество() = 0 Тогда
		Элементы.ДанныеРегистра.ТолькоПросмотр = Ложь;
		Элементы.Товары.ТолькоПросмотр = Ложь;
	КонецЕсли;
```

## Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) 


<details>
<summary> Код </summary>

</details>







