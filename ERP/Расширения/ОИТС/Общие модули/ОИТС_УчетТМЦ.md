# Процедуры

## Процедура ЗаписьВРегистрСведений(ДокСсылка, Товар, Вариант, СсылкаНаПотребление = "", ИзменениеВремени = "") Экспорт 
Процедура создает запись в регистре сведений "Фиксация изменений". 

Вызывается при любом действии с товарами (Передача, ремонт, сборка и тд).

> [!NOTE]
> ИзменениеВремени - необходимо для создания уникальной записи регистра при одинаковых измерениях.
 
<details>
<summary> Код </summary>
        
      НаборЗаписей = РегистрыСведений.ФиксацияИзменений.СоздатьНаборЗаписей();
      НаборЗаписей.Отбор.Регистратор.Установить(ДокСсылка); 
      НаборЗаписей.Прочитать();
      НоваяЗапись = НаборЗаписей.Добавить();
      Если ТипЗнч(ИзменениеВремени) = Тип("Число") Тогда
       НоваяЗапись.Дата = ТекущаяДата() - ИзменениеВремени;
      Иначе	
       НоваяЗапись.Дата = ТекущаяДата();
      КонецЕсли;
      НоваяЗапись.СерийныйНомер = Товар.СерийныйНомер;
      НоваяЗапись.Количество = Товар.Количество;
      НоваяЗапись.Номенклатура = Товар.Номенклатура;
      Если СсылкаНаПотребление = "" ИЛИ  ТипЗнч(ИзменениеВремени) = Тип("Число") Тогда 
       НоваяЗапись.ДокументыРедактирования = Документы.ВнутреннееПотребление.ПустаяСсылка();
      Иначе
       НоваяЗапись.ДокументыРедактирования = СсылкаНаПотребление;
      КонецЕсли;
      Если Вариант = "Передача" Тогда
       НоваяЗапись.Статус = Перечисления.ВариантыСкладскогоУчета.ПередалиВЭксплуатацию;
      ИначеЕсли Вариант = "Списание" Тогда
       НоваяЗапись.Статус = Перечисления.ВариантыСкладскогоУчета.СписалиНаРасходы;
      ИначеЕсли Вариант = "ОтработанРанее" Тогда
       НоваяЗапись.Статус = Перечисления.ВариантыСкладскогоУчета.ОтработанРанее;
      ИначеЕсли Вариант = "Отменили" Тогда
       НоваяЗапись.Статус = Перечисления.ВариантыСкладскогоУчета.Отменили;
      ИначеЕсли Вариант = "ВернулиИзЭксплуатации" Тогда
       НоваяЗапись.Статус = Перечисления.ВариантыСкладскогоУчета.ВернулиИзЭксплуатации;
      ИначеЕсли Вариант = "ВернулиОтработанРанее" Тогда 
       НоваяЗапись.Статус = Перечисления.ВариантыСкладскогоУчета.Вернули_ОтработанРанее;
      ИначеЕсли Вариант = "ИспользовалиДляСборки" Тогда
       НоваяЗапись.Статус = Перечисления.ВариантыСкладскогоУчета.ИспользовалиДляСборки;
      ИначеЕсли Вариант = "СписалиНаРасходы_Ремонт" Тогда
       НоваяЗапись.Статус = Перечисления.ВариантыСкладскогоУчета.СписалиНаРасходы_Ремонт;
      КонецЕсли;
      Если НЕ ТипЗнч(ДокСсылка) = Тип("ДокументСсылка.СборкаТоваров") Тогда		
       НоваяЗапись.ФИО  = ДокСсылка.ФИО;    
      КонецЕсли;
      НоваяЗапись.ТекущийПользователь = Пользователи.ТекущийПользователь(); 
      НаборЗаписей.Записать();
</details>

## Процедура Процедура ВозвратНаСклад(Объект, ТЗСтрок, Строчка) Экспорт
Предназначена для возврата товара от человека обратно на склад, реализовано 2 варианта прихода: 
1. С помощью документа "ПрочееОприходованиеТоваров", если товар передан документом "ПередачаВЭксплуатацию";
2. Создание движения по регистру без привязки к организации".

<details>
<summary> Код </summary>
 
     Если Строчка.ВариантВнесения = Перечисления.ВариантыВнесения.Передача Тогда
     НайденныеСтроки = ТЗСтрок.НайтиСтроки(Новый Структура("ВариантВнесения", Строчка.ВариантВнесения)); 
     Док = Новый ТаблицаЗначений;
     Для Каждого Позиция Из НайденныеСтроки Цикл
      Если Док.Количество() = 0 Тогда
       Док = ЗапросИзРегистраВозврат(Новый Структура("Номенклатура, СерийныйНомер", Позиция.Номенклатура, Позиция.СерийныйНомер)); 	
      Иначе 
       НоваяСтрока = Док.Добавить();
       ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗапросИзРегистраВозврат(Новый Структура("Номенклатура, СерийныйНомер", Позиция.Номенклатура, Позиция.СерийныйНомер))[0]); 
      КонецЕсли;
     КонецЦикла;
     Док.Сортировать("ДокументыРедактирования");
     Пока Док.Количество() > 0 Цикл
      ПоискПоДок = Док.НайтиСтроки(Новый Структура("ДокументыРедактирования", Док[0].ДокументыРедактирования)); 
      ВозвратИзЭксплуатации = Документы.ПрочееОприходованиеТоваров.СоздатьДокумент();
      ВозвратИзЭксплуатации.Дата                  = ТекущаяДата();
      ВозвратИзЭксплуатации.Организация           = Объект.Организация;
      ВозвратИзЭксплуатации.Подразделение         = Справочники.СтруктураПредприятия.НайтиПоНаименованию("Отдел ИТС");
      ВозвратИзЭксплуатации.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратИзЭксплуатации;
      ВозвратИзЭксплуатации.Склад                 = Справочники.Склады.НайтиПоНаименованию("Отдел ИТС (администрирование)");
      ВозвратИзЭксплуатации.Валюта                = Справочники.Валюты.НайтиПоНаименованию("руб.");
      ВозвратИзЭксплуатации.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным;
      СсылкаНового = Документы.ПрочееОприходованиеТоваров.ПолучитьСсылку();
      ВозвратИзЭксплуатации.УстановитьСсылкуНового(СсылкаНового);
      Для Каждого НайденнаяСтрока Из ПоискПоДок Цикл
       ВозвратИзЭксплуатации.ДокументОснование = НайденнаяСтрока.ДокументыРедактирования; 
       Стр = ВозвратИзЭксплуатации.Товары.Добавить();
       ЗаполнитьЗначенияСвойств(Стр, НайденнаяСтрока);  
       Стр.КоличествоУпаковок    = НайденнаяСтрока.Количество; 
       Стр.ФизическоеЛицо        = Объект.ФИО.ФизическоеЛицо;  
       Стр.Цена                  = 100; 
       Стр.Партия                = НайденнаяСтрока.ДокументыРедактирования.Товары[0].Партия;
       Стр.СтатьяРасходовДоходов = ПланыВидовХарактеристик.СтатьиДоходов.НайтиПоНаименованию("Возврат из эксплуатации");
       ОИТС_УчетТМЦ.ЗаписьВРегистрСведений(Объект.Ссылка, 
       Новый Структура("Номенклатура, СерийныйНомер, Количество", НайденнаяСтрока.Номенклатура, 
       НайденнаяСтрока.СерийныйНомер, НайденнаяСтрока.Количество), "ВернулиИзЭксплуатации", ВозвратИзЭксплуатации.ПолучитьСсылкуНового());
       ТЗСтрок.Удалить(ТЗСтрок.Найти(НайденнаяСтрока.СерийныйНомер));
       Док.Удалить(НайденнаяСтрока);
      КонецЦикла;
      Если ВозвратИзЭксплуатации.Товары.Количество() > 0 Тогда 
       ВозвратИзЭксплуатации.Записать(РежимЗаписиДокумента.Проведение);
      КонецЕсли;
     КонецЦикла;
    ИначеЕсли Строчка.ВариантВнесения = Перечисления.ВариантыВнесения.Списание ИЛИ Строчка.ВариантВнесения = Перечисления.ВариантыВнесения.ЗаписьВРегистре Тогда
     Док = Документы.УчетОтработанныхРанееТМЦ.СоздатьДокумент();
     СсылкаНового = Документы.УчетОтработанныхРанееТМЦ.ПолучитьСсылку();
     Док.УстановитьСсылкуНового(СсылкаНового); 
     СсылкаНовойРеализации = Док.ПолучитьСсылкуНового();	
     НайденныеСтроки = ТЗСтрок.НайтиСтроки(Новый Структура("ВариантВнесения", Строчка.ВариантВнесения));
     ИндексСтроки = 0;
     Для Каждого ТекСтрока Из НайденныеСтроки Цикл
      Набор = РегистрыНакопления.ТМЦНаСкладеОИТС.СоздатьНаборЗаписей();
      Набор.Отбор.Регистратор.Установить(СсылкаНовойРеализации);
      Набор.Прочитать();
      ИндексСтроки = ИндексСтроки + 1;
      Движение = Набор.Добавить();
      Движение.ВидДвижения        = ВидДвиженияНакопления.Приход;
      Движение.Период             = ТекущаяДата();
      Движение.НаименованиеТовара = ТекСтрока.Номенклатура;
      Движение.Количество         = ТекСтрока.Количество;
      Движение.ЕдиницыИзмерения   = ТекСтрока.Номенклатура.ЕдиницаИзмерения;
      Движение.СсылкаНаДокумент   = Объект.Ссылка;
      Движение.СерийныйНомер      = ТекСтрока.СерийныйНомер;
      Движение.ОтработанРанее     = Истина;
      Набор.Записать();
      ОИТС_УчетТМЦ.ЗаписьВРегистрСведений(Объект.Ссылка, 
      Новый Структура("Номенклатура, СерийныйНомер, Количество", ТекСтрока.Номенклатура, 
      ТекСтрока.СерийныйНомер, ТекСтрока.Количество), "ВернулиОтработанРанее", СсылкаНовойРеализации, ИндексСтроки);
      ТЗСтрок.Удалить(ТекСтрока);
     КонецЦикла;
    КонецЕсли;
 </details>

# Функции

## Функция ЗапросИзРегистраВозврат(Данные)
Необходима для процедуры ВозвратНаСклад(...), а именно для получения из регистра сведений ссылки на документ "ПередачаВЭксплуатацию"
и сортировке выбранных товаров, которые были добавлены одним документом. В дальнейшем при создании документа "ПрочееОприходованияТоваров"
такие товары будут возвращены вместе и полученная ссылка указывается как документ-основание.

<details>
<summary> Код </summary>
 
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ ПЕРВЫЕ 1
    |	ФиксацияИзменений.ДокументыРедактирования КАК ДокументыРедактирования,
    |	ФиксацияИзменений.СерийныйНомер КАК СерийныйНомер,
    |	ФиксацияИзменений.Номенклатура КАК Номенклатура,
    |	ФиксацияИзменений.Количество КАК Количество
    |ИЗ
    |	РегистрСведений.ФиксацияИзменений КАК ФиксацияИзменений
    |ГДЕ
    |	ФиксацияИзменений.СерийныйНомер = &СерийныйНомер
    |	И ФиксацияИзменений.Номенклатура = &Номенклатура
    |	И ФиксацияИзменений.Статус = &Статус
    |
    |УПОРЯДОЧИТЬ ПО
    |	ФиксацияИзменений.Дата УБЫВ";
    
    Запрос.УстановитьПараметр("Номенклатура", Данные.Номенклатура);
    Запрос.УстановитьПараметр("СерийныйНомер", Данные.СерийныйНомер);
    Запрос.УстановитьПараметр("СерийныйНомер", Данные.СерийныйНомер);
    Запрос.УстановитьПараметр("Статус", Перечисления.ВариантыСкладскогоУчета.ПередалиВЭксплуатацию);
     
    РезультатЗапроса = Запрос.Выполнить();
     
    ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
    
    Возврат ВыборкаДетальныеЗаписи;
</details>

## Функция ЗапросИзРегистра(Данные) Экспорт
Используется для создания движения по регистру накоплений "ТМЦНаСкладеОИТС", ввиду того, что пользоваель указывает серийный номер, необходимо понимать есть ли приход с таким значением, если нет, то расход фиксируется без этого измерения.

<details>
<summary> Код </summary>
 
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |	ТМЦНаСкладеОИТСОстатки.СерийныйНомер КАК СерийныйНомер
    |ИЗ
    |	РегистрНакопления.ТМЦНаСкладеОИТС.Остатки КАК ТМЦНаСкладеОИТСОстатки
    |ГДЕ
    |	ТМЦНаСкладеОИТСОстатки.СерийныйНомер = &СерийныйНомер";
     
    Запрос.УстановитьПараметр("СерийныйНомер", Данные);
     
    РезультатЗапроса = Запрос.Выполнить();
     
    ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
    
    Возврат ВыборкаДетальныеЗаписи;
</details>

